
val String filename = "lichtprogramm.rules"

rule "Schranktaster Arbeitsbeleuchtung ON"
when
    Item FK_Taster_Schrank_2Short changed to ON
then
    if (FK_Arbeit.state == OFF) {
        FK_Arbeit.sendCommand(ON)
    } else if (FK_Edison_Dimmer_Level.state != 70) {
        FK_Edison_Dimmer_Level.sendCommand(70)
    } else {
        Kueche_Deckenlicht.sendCommand(ON)
    }
end

rule "Schranktaster Arbeitsbeleuchtung OFF"
when
    Item FK_Taster_Schrank_1Short changed to ON
then
    FK_Arbeit.sendCommand(OFF)
    Kueche_Deckenlicht.sendCommand(OFF)
    if (FK_Edison_Dimmer_Level.state == 30) FK_Edison_Dimmer_Level.sendCommand(OFF)
    if (FK_Edison_Dimmer_Level.state == 70) FK_Edison_Dimmer_Level.sendCommand(30)
end

rule "Schranktaster Hauptleuchte ON"  when Item FK_Taster_Schrank_2Long changed to ON then Kueche_Deckenlicht.sendCommand(ON) end
rule "Schranktaster Hauptleuchte OFF" when Item FK_Taster_Schrank_1Long changed to ON then Kueche_Deckenlicht.sendCommand(OFF) end


rule "Flur Taster oben kurz"
  when Item FK_Taster_Tokyo_2Short changed to ON or Item FK_Taster_Fuse_2Short changed to ON
  then Whg_Light_Preset.sendCommand(2)
end

rule "Flur Taster oben lang"
  when Item FK_Taster_Tokyo_2Long changed to ON or Item FK_Taster_Fuse_2Long changed to ON
  then Whg_Light_Preset.sendCommand(1)
end

rule "Flur Taster unten kurz"
  when Item FK_Taster_Tokyo_1Short changed to ON or Item FK_Taster_Fuse_1Short changed to ON
  then Whg_Light_Preset.sendCommand(0)
end

rule "Flur Taster unten lang"
  when Item FK_Taster_Tokyo_1Long changed to ON or Item FK_Taster_Fuse_1Long changed to ON
  then Whg_Light_Preset.sendCommand(0)
end


rule "Wohnzimmer Taster oben kurz"
  when Item WZ_Taster_2Short changed to ON
  then Whg_Light_Preset.sendCommand(2)
end

rule "Wohnzimmer Taster unten kurz"
  when Item WZ_Taster_1Short changed to ON
  then Whg_Light_Preset.sendCommand(0)
end


rule "Wohnzimmer Taster Licht an"
when
    Item Fernbedienung_Taste1 received update ON
then
    logInfo (filename, "Fernbedienung 1")
    sendCommand(WZ_Origami, ON)
    sendCommand(WZ_Lacks_LEDs, ON)
    sendCommand(WZ_Expedit, ON)
    sendCommand(BK_Balkonmagic, ON)
end
rule "Wohnzimmer Taster Licht aus"
when
    Item Fernbedienung_Taste2 received update ON
then
    logInfo (filename, "Fernbedienung 2")
    sendCommand(WZ_Origami, OFF)
    sendCommand(WZ_Lacks_LEDs, OFF)
    sendCommand(WZ_Expedit, OFF)
    sendCommand(BK_Balkonmagic, OFF)
end


rule "Whg_Light_Preset (Sitemap -> Homematic)"
when
  Item Whg_Light_Preset received command
then
    //logInfo(filename, "Whg_Light_Preset (Sitemap -> Homematic).")
    switch (receivedCommand){
        case 0: {
            gLight.members.filter[item | item.state != OFF].forEach[item | item.sendCommand(OFF)]
            logInfo(filename, "Lichtprogramm 0 - Aus")
        }
        case 1: {
            logInfo(filename, "Lichtprogramm 1 - Normal")
            BK_Balkonmagic.sendCommand(ON)
            WZ_Lacks_LEDs.sendCommand(ON)
            WZ_Origami.sendCommand(ON)
            WZ_Expedit.sendCommand(ON)
            FK_Arbeit.sendCommand(ON)
            FK_Edison_Dimmer_Level.sendCommand(70)
            WZ_Goldfruchtpalme.sendCommand(OFF)
            WZ_Lacks_Lampe.sendCommand(OFF)
            Flur_Deckenlicht.sendCommand(OFF)
            Kueche_Deckenlicht.sendCommand(OFF)
        }
        case 2: {
            logInfo(filename, "Lichtprogramm 2 - Relax")
            BK_Balkonmagic.sendCommand(ON)
            WZ_Lacks_LEDs.sendCommand(ON)
            WZ_Origami.sendCommand(ON)
            WZ_Expedit.sendCommand(ON)
            FK_Arbeit.sendCommand(ON)
            FK_Edison_Dimmer_Level.sendCommand(30)
            WZ_Goldfruchtpalme.sendCommand(OFF)
            WZ_Lacks_Lampe.sendCommand(OFF)
            Flur_Deckenlicht.sendCommand(OFF)
            Kueche_Deckenlicht.sendCommand(OFF)
            SZ_Deckenlicht.sendCommand(OFF)
        }
        case 3: {
            gLight.members.filter[item | item.state != ON].forEach[item | item.sendCommand(ON)]
            logInfo(filename, "Lichtprogramm 3 - Voll")
        }
    }
end

// vim: syntax=Xtend
