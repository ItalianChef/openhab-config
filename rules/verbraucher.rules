import java.util.concurrent.locks.ReentrantLock

var java.util.concurrent.locks.ReentrantLock finishLock  = new java.util.concurrent.locks.ReentrantLock()

val MODE_OFF = 0
val MODE_STANDBY = 1
val MODE_ACTIVE = 2
val MODE_FINISHED = 3

rule "Waschmaschine Überwachung: Init"
when
    System started
then
    createTimer(now.plusSeconds(170)) [|
        if (BD_Waschmaschine_OpState == NULL) BD_Waschmaschine_OpState.postUpdate(MODE_OFF)
		if (FK_Spuelmaschine_OpState == NULL) FK_Spuelmaschine_OpState.postUpdate(MODE_OFF)
    ]
end

/**
 * Waschmaschine
 * Aus: 0,05 - 0,07 W
 * Standby/Fertig: 4,3W
 * Aktiv: bis 2300W (bisher gesehener Mindestwert 4,55W)
 */
rule "Waschmaschine Überwachung: Verbrauchszuordnung"
when
    Item BD_Waschmaschine_Power changed
then
    //logInfo("RULE", "Waschmaschine Überwachung: Verbrauchszuordnung")
    if (BD_Waschmaschine_Power.state < 0.2) BD_Waschmaschine_OpState.postUpdate(MODE_OFF)
    else if (BD_Waschmaschine_Power.state > 10) BD_Waschmaschine_OpState.postUpdate(MODE_ACTIVE)
	else if (BD_Waschmaschine_Power.state < 4.5) {
		if (BD_Waschmaschine_OpState.state == MODE_OFF) BD_Waschmaschine_OpState.postUpdate(MODE_STANDBY)
		else if (BD_Waschmaschine_OpState.state == MODE_ACTIVE) {
			finishLock.lock()
			try {
				// Debounce for 30 seconds
				Thread::sleep(30000)
				if (BD_Waschmaschine_Power.state < 4.5) BD_Waschmaschine_OpState.postUpdate(MODE_FINISHED)
			} finally {
				finishLock.unlock()
			}
		}
    }
end

rule "Waschmaschine Überwachung: Benachrichtigung"
when
    Item BD_Waschmaschine_OpState changed
then
    if (BD_Waschmaschine_OpState.state == MODE_FINISHED) {
    	pushover("Waschmaschine fertig!")
    }
end

//TODO: Power wechselt beim Einschalten mehrfach zwischen ACTIVE and FINISHED, Timer einbauen!
// 2016-08-05 15:38:53.390 [ItemStateChangedEvent     ] - FK_Spuelmaschine_Power changed from 66.31 to 0.70
// 2016-08-05 15:39:01.490 [ItemStateChangedEvent     ] - FK_Spuelmaschine_Power changed from 0.70 to 45.25
// 2016-08-05 15:39:09.411 [ItemStateChangedEvent     ] - FK_Spuelmaschine_Power changed from 45.25 to 141.67
// 2016-08-05 15:39:17.456 [ItemStateChangedEvent     ] - FK_Spuelmaschine_Power changed from 141.67 to 0.68
// 2016-08-05 15:39:25.401 [ItemStateChangedEvent     ] - FK_Spuelmaschine_Power changed from 0.68 to 167.85
// 2016-08-05 15:39:34.170 [ItemStateChangedEvent     ] - FK_Spuelmaschine_Power changed from 167.85 to 91.66
// 2016-08-05 15:39:41.419 [ItemStateChangedEvent     ] - FK_Spuelmaschine_Power changed from 91.66 to 0.68
// 2016-08-05 15:39:49.411 [ItemStateChangedEvent     ] - FK_Spuelmaschine_Power changed from 0.68 to 160.08
 
rule "Spuelmaschine Überwachung: Verbrauchszuordnung"
when
    Item FK_Spuelmaschine_Power changed
then
    //logInfo("RULE", "Waschmaschine Überwachung: Verbrauchszuordnung")
    if (FK_Spuelmaschine_Power.state < 0.03) FK_Spuelmaschine_OpState.postUpdate(MODE_OFF)
    else if (FK_Spuelmaschine_Power.state > 5) FK_Spuelmaschine_OpState.postUpdate(MODE_ACTIVE)
	else if (FK_Spuelmaschine_Power.state < 0.75) {
		if (FK_Spuelmaschine_OpState.state == MODE_OFF) FK_Spuelmaschine_OpState.postUpdate(MODE_STANDBY)
		else if (FK_Spuelmaschine_OpState.state == MODE_ACTIVE) FK_Spuelmaschine_OpState.postUpdate(MODE_FINISHED)
    }
end

// vim: syntax=Xtend
